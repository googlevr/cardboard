apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.0"
    lintOptions {
        abortOnError false
    }
    defaultConfig {
        // #gles3 - minSdkVersion is only 18 for OpenGL ES 3.0 support. You can
        // reduce minSDKVersion to 16 if only OpenGL ES 2.0 is required by your
        // app. See the release notes for details.
        minSdkVersion 18
        targetSdkVersion 30
        versionCode 1
        versionName "1.4.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
        externalNativeBuild {
            cmake {
                cppFlags.add("-std=c++17")
                arguments "-DANDROID_STL=c++_static"
            }
        }
        defaultConfig {
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    sourceSets {
        // Sets path to java, jni, resources files and manifest location as it is not the default one.
        main {
            manifest.srcFile 'qrcode/android/AndroidManifest.xml'
            java.srcDirs = ['qrcode/android/java', 'device_params/android/java', 'screen_params/android/java']
            jni.srcDirs = ['qrcode/android/jni']
            res.srcDirs = ['qrcode/android/res']
        }
        // Adds proto file and generated source files
        main.java.srcDirs += "${protobuf.generatedFilesBaseDir}/main/javalite"
        main.proto.srcDirs = ["${project.rootDir}/proto"]
        main.proto.includes = ["cardboard_device.proto"]
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    // Android Mobile Vision
    implementation 'com.google.android.gms:play-services-vision:15.0.2'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.google.protobuf:protobuf-lite:3.0.0'
}
